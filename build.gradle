buildscript{
    dependencies{
        classpath "com.github.Anuken.Arc:arc-core:$arcVersion"
    }

    repositories{
        // Necessary Maven repositories for build script classpath.
        mavenCentral()
        maven{url 'https://www.jitpack.io'}
        maven{url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository'}
    }
}

import arc.files.*
import arc.graphics.*
import arc.util.*
import arc.util.io.*
import arc.util.serialization.*
import groovy.transform.*

import java.util.concurrent.atomic.*

configure(allprojects){
    apply plugin: 'java'

    // Main source directory.
    sourceSets.main.java.srcDirs = ['src/']

    dependencies{
        // Downgrade Java 9+ syntax into being available in Java 8.
        annotationProcessor "com.github.GlennFolker.EntityAnno:downgrader:$entVersion"
    }

    repositories{
        // Necessary Maven repositories to pull the dependencies.
        mavenCentral()
        maven{url 'https://www.jitpack.io'}
        maven{url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository'}
    }

    // Use Java 17 syntax, but target Java 8 bytecode version.
    tasks.withType(JavaCompile).configureEach{
        sourceCompatibility = 17
        options.release = 8

        options.incremental = true
        options.encoding = 'UTF-8'
    }
}

@CompileStatic
void fetchSprites(String mindustryVersion, File baseDir){
    final def amount = new AtomicInteger(1), total = new AtomicInteger(0)
    Http.get("https://api.github.com/repos/Anuken/Mindustry/contents/core/assets-raw/sprites/units?ref=$mindustryVersion")
        .timeout(0)
        .error{throw new RuntimeException(it)}
        .block{response(baseDir, amount, total, it)}

    while(amount.get() > 0) Thread.yield()
    println "Fetched ${total.get()} unit sprites"
}

@CompileStatic
void response(File dir, AtomicInteger amount, AtomicInteger total, Http.HttpResponse res){
    final def list = Jval.read(res.resultAsString).asArray()
    amount.addAndGet(list.size)

    for(final def val : list){
        final def name = val.getString('name')
        switch(val.getString('type')){
            case 'file':
                Http.get(val.getString('download_url'))
                    .timeout(0)
                    .error{throw new RuntimeException(it)}
                    .submit{
                        if(name.endsWith('.png')){
                            final def image = new Pixmap(Streams.copyBytes(it.resultAsStream))
                            new Fi(file("$dir/$name")).writePng(image)
                        }

                        total.addAndGet(1)
                        amount.addAndGet(-1)
                    }
                break
            case 'dir':
                Http.get(val.getString('url'))
                    .timeout(0)
                    .error{throw new RuntimeException(it)}
                    .submit{response(file("$dir/$name"), amount, total, it)}
                break
        }
    }

    amount.addAndGet(-1)
}

configure(project(':proc')){
    tasks.register('fetchSprites'){
        final def fetchOutputProv = layout.buildDirectory.file('fetched')
        doFirst{
            def fetchOutput = fetchOutputProv.get().asFile
            fetchOutput.deleteDir()
            fetchOutput.mkdir()

            fetchSprites(mindustryVersion, fetchOutput)
            file("$fetchOutput/cache.txt").text = mindustryVersion
        }

        outputs.upToDateWhen{
            def fetchOutput = fetchOutputProv.get().asFile
            def cache = file("$fetchOutput/cache.txt")
            cache.exists() && cache.text == mindustryVersion
        }
    }

    tasks.register('genSprites', JavaExec){
        dependsOn fetchSprites
        dependsOn configurations.runtimeClasspath

        final def genOutputProv = layout.buildDirectory.file('output')
        final def fetchSpritesProv = layout.buildDirectory.file('fetched')

        mainClass = 'lights.gen.Generator'
        classpath = sourceSets.main.runtimeClasspath
        standardInput = System.in
        workingDir = genOutputProv

        doFirst{
            def genOutput = genOutputProv.get().asFile
            genOutput.deleteDir()
            genOutput.mkdir()

            args fetchSpritesProv.get().asFile
        }

        doLast{
            def genOutput = genOutputProv.get().asFile
            file("$genOutput/cache.txt").text = mindustryVersion
        }

        outputs.upToDateWhen{
            def genOutput = genOutputProv.get().asFile
            def cache = file("$genOutput/cache.txt")
            cache.exists() && cache.text == mindustryVersion
        }
    }

    dependencies{
        // Depend on Mindustry/Arc classpaths.
        implementation "com.github.Anuken.Mindustry:core:$mindustryVersion"
        implementation "com.github.Anuken.Arc:arc-core:$arcVersion"
        implementation "com.github.Anuken.Arc:natives-desktop:$arcVersion"
    }
}

configure(rootProject){
    jar{
        dependsOn project(':proc').tasks.genSprites
        archiveFileName = "${rootProject.name}-Desktop.jar"

        from files(sourceSets.main.output.classesDirs)
        from files(sourceSets.main.output.resourcesDir)
        from configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}

        from files(layout.projectDirectory.file('assets'))
        from files(project(':proc').layout.projectDirectory.file('output')){
            exclude 'cache.txt'
        }
    }

    tasks.register('dex', Jar){
        dependsOn jar
        archiveFileName = "${rootProject.name}.jar"

        final def desktopJar = (jar as Jar).archiveFile.get().asFile
        final def dexJar = file("$dex.temporaryDir/Dexed.jar")
        doFirst{
            def command = "d8 --min-api $sdkAPI --output $dexJar $desktopJar"

            (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList()).forEach{
                if(it.exists()) command += " --classpath $it.path"
            }

            def sdkRoot = System.getenv('ANDROID_SDK_ROOT') ?: System.getenv('ANDROID_HOME')
            command += " --lib ${file("$sdkRoot/platforms/android-$sdkVersion/android.jar")}"

            if(System.getProperty('os.name').contains('Windows')) command = "cmd /c $command"
            command.execute(null, layout.buildDirectory.file('libs').get().asFile).waitForProcessOutput(System.out, System.err)
        }

        from zipTree(desktopJar)
        from zipTree(dexJar)
    }

    dependencies{
        // Depend on Mindustry/Arc classpaths.
        compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
        compileOnly "com.github.Anuken.Arc:arc-core:$arcVersion"
    }
}
